{
    parserClass = "com.github.madwareru.intellijsignificantwhitespacelang.language.parser._PyPasParser"
    parserUtilClass = "com.github.madwareru.intellijsignificantwhitespacelang.language.psi.PyPasParserUtil"

    extends = "com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix = "PyPas"
    psiImplClassSuffix = "Impl"
    psiPackage = "com.github.madwareru.intellijsignificantwhitespacelang.language.psi"
    psiImplPackage = "com.github.madwareru.intellijsignificantwhitespacelang.language.psi.impl"

    elementTypeHolderClass = "com.github.madwareru.intellijsignificantwhitespacelang.language.psi.PyPasTypes"
    elementTypeClass = "com.github.madwareru.intellijsignificantwhitespacelang.language.psi.PyPasElementType"
    tokenTypeClass = "com.github.madwareru.intellijsignificantwhitespacelang.language.psi.PyPasTokenType"

    psiImplUtilClass = "com.github.madwareru.intellijsignificantwhitespacelang.language.psi.impl.PyPasPsiImplUtil"

    tokens = [
            COMMENT = "regexp://.*"
            BOOLEAN = "regexp:true|false"
            IDENT = "regexp:[A-Za-z_][A-Za-z0-9_]*"
            INTEGER = "regexp:[+-]?[0-9][0-9_]*"
            FLOAT = "regexp:([+-]?[0-9]+\.[0-9]*([Ee][0-9]+)?)|(\.[0-9]+([Ee][0-9]+)?)"
            CHAR = "CHAR"
            STRING = "STRING"
            INDENT = "INDENT"
            UNINDENT = "UNINDENT"
            NEW_LINE = "NEW_LINE"

            PARENTHESISL = "("
            PARENTHESISR = ")"
            BRACKETL = "["
            BRACKETR = "]"
            BRACEL = "{"
            BRACER = "}"
            COLON = ":"
            SEMICOLON = ";"
            COMMA = ","
            DOT = "."
            AND = "and"
            OR = "or"
            NOT = "not"
            BITWISE_AND = "&"
            BITWISE_OR = "|"
            BITWISE_NOT = "~"
            BITWISE_XOR = "^"
            PLUS = "+"
            MINUS = "-"
            DIV = "/"
            MOD = "%"
            MUL = "*"
            ASSIGNMENT_OP = ":="
            NEQ_OP = "!="
            EQ_OP = "="
            LT_OP = "<"
            HT_OP = ">"
            LTEQ_OP = "<="
            HTEQ_OP = ">="
            VAR = "var"
            LET = "let"
            MODULE = "mod"
            FUNCTION = "fn"
            PROCEDURE = "proc"
            IF = "if"
            ELIF = "elif"
            ELSE = "else"
            FOR = "for"
            TO = "to"
            WHILE = "while"
            AS = "as"
            TYPE = "type"
            RECORD = "record"
            ARRAY = "array"
            RETURN = "return"
            OF = "of"
        ]
}

PyPas ::= program_header definitions
program_header ::= MODULE IDENT
definitions ::= COLON NEW_LINE INDENT definition+ UNINDENT
definition ::= proc_define | func_define | record_define
proc_define ::= PROCEDURE IDENT PARENTHESISL [arg_defs] PARENTHESISR block_statements_body
func_define ::= FUNCTION IDENT PARENTHESISL [arg_defs] PARENTHESISR MINUS HT_OP IDENT block_statements_body
block_statements_body ::= COLON NEW_LINE INDENT [statements] UNINDENT
var_definitions ::= (var_definition NEW_LINE)+
var_definitions_single_line ::= var_definition (COMMA var_definition)*
let_definitions ::= (let_definition NEW_LINE)+
let_definitions_single_line ::= let_definition (COMMA let_definition)*
var_definition ::= IDENT COLON type_definition [ASSIGNMENT_OP operator_expr]
let_definition ::= IDENT COLON type_definition ASSIGNMENT_OP operator_expr
arg_definition ::= IDENT COLON type_definition [EQ_OP operator_expr]
type_definition ::= simple_type_definition | array_type_definition
simple_type_definition ::= IDENT
array_type_definition ::= ARRAY BRACKETL INTEGER TO INTEGER BRACKETR OF IDENT
statements ::= statement+
statement ::= (single_line_statement (SEMICOLON single_line_statement)* NEW_LINE)
    | if_statement
    | for_statement
    | while_statement
    | var_statement
    | let_statement

single_line_statement ::= assignment_statement | function_invocation | return_statement | var_statement_single_line | let_statement_single_line

if_statement ::= IF operator_expr block_statements_body
    (ELIF operator_expr block_statements_body)*
    (ELSE block_statements_body)?

var_statement ::= VAR NEW_LINE INDENT var_definitions UNINDENT
var_statement_single_line ::= VAR var_definitions_single_line

let_statement ::= LET NEW_LINE INDENT let_definitions UNINDENT
let_statement_single_line ::= LET let_definitions_single_line

for_statement ::= FOR IDENT ASSIGNMENT_OP constant_literal to constant_literal block_statements_body
while_statement ::= WHILE operator_expr block_statements_body
return_statement ::= RETURN operator_expr
assignment_statement ::= lvalue ASSIGNMENT_OP operator_expr
lvalue ::= subfield_access | indexed_access | IDENT
constant_literal ::= BOOLEAN | INTEGER | FLOAT | STRING
variable_access ::= IDENT
subfield_access ::= IDENT (DOT IDENT)+
indexed_access ::= IDENT (BRACKETL operator_expr BRACKETR)+
function_invocation ::= IDENT PARENTHESISL [args] PARENTHESISR

term_expr ::= (PARENTHESISL operator_expr PARENTHESISR)
    | function_invocation
    | indexed_access
    | subfield_access
    | constant_literal
    | variable_access

e_operator ::= (PLUS | MINUS | MUL | DIV | MOD | BITWISE_NOT | BITWISE_OR | BITWISE_XOR | BITWISE_AND
    | LT_OP | LTEQ_OP | HT_OP | HTEQ_OP | EQ_OP | NEQ_OP | NOT | OR | AND)

operator_expr ::= term_expr (e_operator operator_expr)*

arg_defs ::= arg_definition (COMMA arg_definition)*
args ::= operator_expr (COMMA operator_expr)*
record_define ::= RECORD IDENT record_body
record_body ::= COLON NEW_LINE INDENT var_definitions UNINDENT